plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def signingProperties = new Properties()
def signingPropertiesFile = rootProject.file('signing.properties')
if (signingPropertiesFile.exists()) {
    signingPropertiesFile.withReader('UTF-8') {
        signingProperties.load(it)
    }
}
def _keyAlias = signingProperties.getProperty('key.alias')
def _keyPassword = signingProperties.getProperty('key.password')
def _storePassword = signingProperties.getProperty('store.password')

def _naverClientId = signingProperties.getProperty('naver.client.id')
def _naverClientSecret = signingProperties.getProperty('naver.client.secret')
def _naverClientName = signingProperties.getProperty('naver.client.name')

android {
    namespace 'com.yeolsimee.moneysaving'
    compileSdk 33

    defaultConfig {
        applicationId "com.yeolsimee.moneysaving"
        minSdk 24
        targetSdk 33
        versionCode 13
        versionName "1.0.13"
        ndk.debugSymbolLevel 'FULL'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "OAUTH_CLIENT_ID", "\"$_naverClientId\""
        buildConfigField "String", "OAUTH_CLIENT_SECRET", "\"$_naverClientSecret\""
        buildConfigField "String", "OAUTH_CLIENT_NAME", "\"$_naverClientName\""

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            keyAlias _keyAlias
            keyPassword _keyPassword
            storeFile file('../keystore/moneysaving_key')
            storePassword _storePassword
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    implementation project(path: ':data')
    implementation project(path: ':domain')

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.compose.material3:material3:1.2.0-alpha01'
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.1"

    // ui
    implementation "io.coil-kt:coil-compose:2.3.0"
    implementation "io.coil-kt:coil-svg:2.3.0"
    implementation 'androidx.core:core-splashscreen:1.0.1'

    // status bar color
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.25.1"

    // Firebase 로그인, Functions, Dynamic Links
    implementation platform('com.google.firebase:firebase-bom:32.0.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-functions-ktx'
    implementation 'com.google.firebase:firebase-dynamic-links-ktx'

    // Google 로그인
    implementation 'com.google.android.gms:play-services-auth:20.5.0'

    // naver
    implementation "com.navercorp.nid:oauth:5.5.0"

    // Jetpack LiveData
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    // ViewModel-Compose 사용
    var lifecycle_version = "2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // hilt, dagger
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // orbit mvi
    implementation 'org.orbit-mvi:orbit-compose:4.6.1'
    implementation 'org.orbit-mvi:orbit-viewmodel:4.6.1'

    // orbit mvi Tests
    testImplementation("org.orbit-mvi:orbit-test:4.6.1")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // kotest
    testImplementation 'io.kotest:kotest-runner-junit5:4.3.2'
}

configurations.all {
    exclude group: 'com.google.android.gms', module: 'play-services-safetynet'
}
