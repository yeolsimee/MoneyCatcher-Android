plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def serverProperties = new Properties()
def serverPropertiesFile = rootProject.file('server.properties')
if (serverPropertiesFile.exists()) {
    serverPropertiesFile.withReader('UTF-8') {
        serverProperties.load(it)
    }
}

def mockUrl = serverProperties.getProperty('MOCK_URL')
def realUrl = serverProperties.getProperty('REAL_URL')

android {
    namespace 'com.yeolsimee.moneysaving.data'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "String", "MOCK_URL", "\"$mockUrl\""
        buildConfigField "String", "REAL_URL", "\"$realUrl\""
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

dependencies {
    implementation project(path: ':domain')

    var hilt_version = '2.46.1'
    implementation 'androidx.core:core-ktx:1.10.1'

    // Firebase Auth
    implementation platform('com.google.firebase:firebase-bom:32.1.1')
    implementation 'com.google.firebase:firebase-auth-ktx'

    // Retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // hilt, dagger
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'

    // room DB
    var room_version = "2.5.1"
    implementation("androidx.room:room-runtime:$room_version")
    annotationProcessor("androidx.room:room-compiler:$room_version")
    kapt("androidx.room:room-compiler:$room_version")

    // datastore
    implementation "androidx.datastore:datastore:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'
    testImplementation "androidx.arch.core:core-testing:2.2.0"

    // kotest
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.8.10"
    testImplementation 'io.kotest:kotest-runner-junit5:5.6.2'
    testImplementation group: 'io.mockk', name: 'mockk', version: '1.13.5'

}

configurations.all {
    exclude group: 'com.google.android.gms', module: 'play-services-safetynet'
}